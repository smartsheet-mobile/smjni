/*
 Copyright 2014 Smartsheet Inc.
 Copyright 2019 SmJNI Contributors

 Licensed under the Apache License, Version 2.0 (the "License");
 you may not use this file except in compliance with the License.
 You may obtain a copy of the License at

     http://www.apache.org/licenses/LICENSE-2.0

 Unless required by applicable law or agreed to in writing, software
 distributed under the License is distributed on an "AS IS" BASIS,
 WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 See the License for the specific language governing permissions and
 limitations under the License.
*/

plugins {
    id 'java'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_1_8

sourceSets {
    main.java.srcDirs = ['src']
}

jar {
    manifest {
        attributes 'Implementation-Title': 'SmJNI Code Generation Annotations',
                   'Implementation-Version': project.version
    }

    setArchiveFileName("jnigen-annotations.jar")
    setDestinationDirectory(rootProject.buildDir)
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}

task javadocJar(type: Jar) {
    from javadoc
    classifier 'javadoc'
}

def libraryDescription = "Annotations used by SmJNI JniGen annotation processor to generate C++ code"
def libraryWebsiteUrl = 'https://github.com/smartsheet-mobile/smjni'
def libraryVcsUrl = 'https://github.com/smartsheet-mobile/smjni.git'
def libraryIssueTrackerUrl = 'https://github.com/smartsheet-mobile/smjni/issues'


publishing {
    if (project.hasProperty("localRepo")) {

        repositories {
            maven {
                name "localRepo"
                url project.getProperty("localRepo")
            }
        }
    }
    publications {
        thePublication(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar
            pom {
                name = "SmJNI Code Generation Annotations"
                description = libraryDescription
                url = libraryWebsiteUrl
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                scm {
                    connection = "git@github.com:smartsheet-mobile/smjni.git"
                    developerConnection = "git@github.com:smartsheet-mobile/smjni.git"
                    url = libraryVcsUrl
                }
            }
        }
    }
}

